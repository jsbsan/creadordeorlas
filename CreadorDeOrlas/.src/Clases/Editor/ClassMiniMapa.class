' Gambas class file

Private hcoeficientePantalla As Float
Private fotosRectangulares As New ClassRectanguloRelleno[]
Private rectanguloh As Float
Private rectangulow As Float

Private hvista As ScrollView
Private hareamayor As DrawingArea
Private hminimap As DrawingArea
Private hcoeficiente As Float
Property imagenF As Image 
Private himagenF As Image 

Public Sub configuro(vista As ScrollView, areamayor As DrawingArea, minimap As DrawingArea, coeficiente As Float)
  
  Dim relacion As Float
  Dim hbrush As PaintBrush
  
  hcoeficiente = coeficiente
  hvista = vista
  hareamayor = areamayor
  hminimap = minimap
  hminimap.Cached = True
  hminimap.border = 1
  relacion = areamayor.h / areamayor.W * coeficiente
  MiniMap.h = relacion * MiniMap.w
  
  rectanguloW = vista.w / (AreaMayor.w * coeficiente) * MiniMap.W 
  rectanguloH = vista.h / (AreaMayor.h * coeficiente) * MiniMap.h
  
  Paint.Begin(minimap)
  borradoPantalla()
  
  hbrush = paint.Image(himagenF)
  hbrush.Translate(0, 0)
  hbrush.Scale(minimap.w / himagenF.w, minimap.w / himagenF.w) '(200 / himage.w, 200 / himage.h)
  
  Paint.Brush = hbrush
  Paint.Rectangle(0, 0, minimap.w, minimap.h)
  Paint.Fill()
  
  Paint.brush = Paint.Color(Color.red)
  Paint.LineWidth = 1
  Paint.Rectangle(1, 1, rectanguloW - 2, rectanguloH - 2)
  Paint.stroke
  dibujoFotos() 
  paint.End
  hcoeficientePantalla = MiniMap.W / (AreaMayor.w * coeficiente) 
  
End

Public Sub movimientoRaton(ratonX As Integer, ratonY As Integer)
  
  hvista.ScrollX = ratonX * hAreaMayor.w / hMiniMap.W * hcoeficiente
  hvista.scrolly = ratony * hAreaMayor.h / hMiniMap.h * hcoeficiente
  
End

Public Sub movimientoVentana()
  
  Dim hbrush As PaintBrush
  Dim xx As Float, yy As Float
  'comprobacion de que no sea NULL el hminimap
  If IsNull(hminimap) Then Return 
  
  Paint.Begin(hminimap)
  borradoPantalla()
  
  hbrush = paint.Image(himagenF)
  hbrush.Translate(0, 0)
  hbrush.Scale(hminimap.w / himagenF.w, hminimap.w / himagenF.w) '(200 / himage.w, 200 / himage.h)
  '
  Paint.Brush = hbrush
  Paint.Rectangle(0, 0, hminimap.w, hminimap.h)
  Paint.Fill()
  
  'ScrollView1.H, ScrollView1.W, ScrollView1.ScrollX, scrollview1.scrolly, DrawingAreaMiniMundo.h, DrawingAreaMiniMundo.w, drawingArea1.h, drawingArea1.w)
  'h As Integer, w As Integer, ScrollX As Integer, scrolly As Integer, minih As Integer, miniw As Integer, maph As Integer, mapw As Integer)
  xx = hvista.ScrollX * hminimap.w / (hAreaMayor.w * hcoeficiente)
  yy = hvista.ScrollY * hminimap.h / (hAreaMayor.h * hcoeficiente)
  '(h As Integer, w As Integer, ScrollX As Integer, scrolly As Integer, minih As Integer, miniw As Integer, maph As Integer, mapw As Integer)
  
  Paint.brush = Paint.Color(Color.red)
  Paint.LineWidth = 1
  Paint.Rectangle(xx + 1, yy + 1, rectangulow - 2, rectanguloh - 2)
  Paint.stroke
  dibujoFotos() 
  paint.End
  
End

Public Sub borradoPantalla()
  
  paint.Rectangle(0, 0, hminimap.w, hminimap.h) '1250 para -> 210
  Paint.LineWidth = 0.1
  Paint.Brush = Paint.Color(Color.white)
  Paint.Fill(True)
  
  paint.Stroke
  
End

Private Function imagenF_read() As Image 
  
  Return himagenF 
  
End  

Private Sub imagenF_write(Valor As Image)
  
  himagenF = Valor 
  
End

Public Sub annadeRectanguloRellenos(ArrayFotosRectangulos As ClassRectanguloFijo[])
  
  Dim a As Integer
  Dim fotoTemp As New ClassRectanguloRelleno
  
  For a = 0 To ArrayFotosRectangulos.count - 1
    fototemp.oficio = ArrayFotosRectangulos[a].oficio
    fototemp.pto1 = ArrayFotosRectangulos[a].pto1
    
    fotosRectangulares.add(fototemp)
    fototemp = New ClassRectanguloRelleno
    
  Next
  
End

Private Function dibujoFotos() 
  
  Dim hcolorlinea As Integer
  Dim a As Integer
  
  For a = 0 To fotosRectangulares.count - 1
    If fotosRectangulares[a].oficio = "alumno" Then 
      hcolorlinea = Color.Orange
    Else 
      hcolorlinea = Color.green
    Endif
    Paint.brush = Paint.Color(hcolorlinea)
    Paint.LineWidth = 1
    Paint.Rectangle((fotosRectangulares[a].pto1.x - 30) * hcoeficientePantalla, (fotosRectangulares[a].pto1.y - 30) * hcoeficientePantalla, 143 * hcoeficientePantalla, 183 * hcoeficientePantalla)
    Paint.stroke
  Next
  
End
